SELF := justfile()
ROOT := parent_directory(justfile_directory())

install:
  @just --working-directory {{ROOT}} -f {{SELF}} link

watch:
  #!/usr/bin/env bash
  echo "Watching for changes... (Ctrl+C to stop)"
  touch ~/.config/.dotfiles_last_sync
  while true; do
    find src fonts -type f -newer ~/.config/.dotfiles_last_sync 2>/dev/null | head -1 | grep -q . && {
      just --working-directory {{ROOT}} -f {{SELF}} link
      touch ~/.config/.dotfiles_last_sync
    }
    sleep 1
  done

link:
  #!/usr/bin/env bash
  set -euo pipefail
  shopt -s dotglob
  find src -type f | while read -r file; do
    just --working-directory {{ROOT}} -f {{SELF}} __link "$file"
  done
  # Copy fonts folder
  mkdir -p ~/.config/wezterm/fonts
  for f in fonts/*; do
    rm -f ~/.config/wezterm/fonts/"$(basename "$f")"
    cp "$f" ~/.config/wezterm/fonts/"$(basename "$f")"
  done
  echo "Copied fonts"
  touch ~/.config/.dotfiles_last_sync

__link src:
  #!/usr/bin/env bash
  set -euo pipefail
  first_line=$(head -n 1 "{{src}}" | tr -d '\r')
  [[ ! "${first_line:0:5}" =~ "@" ]] && exit 0
  dest=$(eval echo "${first_line#*@}")
  [ -z "$dest" ] && exit 1
  mkdir -p "$(dirname "$dest")"
  rm -f "$dest"
  cp "{{src}}" "$dest"
  echo "Copied $(basename "{{src}}")"
