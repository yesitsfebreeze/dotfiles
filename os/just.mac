SELF := justfile()
ROOT := parent_directory(justfile_directory())

install:
  @just --working-directory {{ROOT}} -f {{SELF}} __brew
  @just --working-directory {{ROOT}} -f {{SELF}} __update wezterm true
  @just --working-directory {{ROOT}} -f {{SELF}} __update hammerspoon true
  @just --working-directory {{ROOT}} -f {{SELF}} __update nvim false
  @just --working-directory {{ROOT}} -f {{SELF}} __update ripgrep false
  @just --working-directory {{ROOT}} -f {{SELF}} link
  @just --working-directory {{ROOT}} -f {{SELF}} __adjust

watch:
  #!/usr/bin/env bash
  echo "Watching for changes... (Ctrl+C to stop)"
  touch ~/.config/.dotfiles_last_sync
  while true; do
    find src fonts -type f -newer ~/.config/.dotfiles_last_sync 2>/dev/null | head -1 | grep -q . && {
      just --working-directory {{ROOT}} -f {{SELF}} link
      touch ~/.config/.dotfiles_last_sync
    }
    sleep 1
  done

link:
  #!/usr/bin/env bash
  set -euo pipefail
  shopt -s dotglob
  find src -type f | while read -r file; do
    just --working-directory {{ROOT}} -f {{SELF}} __link "$file"
  done
  # Copy fonts folder
  mkdir -p ~/.config/wezterm/fonts
  for f in fonts/*; do
    rm -f ~/.config/wezterm/fonts/"$(basename "$f")"
    cp "$f" ~/.config/wezterm/fonts/"$(basename "$f")"
  done
  echo "Copied fonts"
  touch ~/.config/.dotfiles_last_sync

__link src:
  #!/usr/bin/env bash
  set -euo pipefail
  first_line=$(head -n 1 "{{src}}" | tr -d '\r')
  [[ ! "${first_line:0:5}" =~ "@" ]] && exit 0
  dest=$(eval echo "${first_line#*@}")
  [ -z "$dest" ] && exit 1
  mkdir -p "$(dirname "$dest")"
  rm -f "$dest"
  cp "{{src}}" "$dest"
  echo "Copied $(basename "{{src}}")"

__brew:
  #!/usr/bin/env bash
  set -euo pipefail
  if ! command -v brew >/dev/null 2>&1; then
    bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
  else
    brew update
    brew upgrade
  fi

__update p cask:
  #!/usr/bin/env bash
  set -euo pipefail
  if ! brew list {{p}} >/dev/null 2>&1; then
    if [ "{{cask}}" = "true" ]; then
      brew install --cask {{p}}
    else
      brew install {{p}}
    fi
  else
    brew upgrade {{p}}
  fi

__adjust:
  @defaults write -g NSAutomaticWindowAnimationsEnabled -bool false
  @defaults write -g NSWindowResizeTime -float 0.001
  @defaults write -g NSScrollAnimationEnabled -bool false
  @defaults write -g NSDocumentRevisionsWindowTransformAnimation -bool false
  @defaults write -g NSInitialToolTipDelay -int 0
  @defaults write -g NSWindowResizeTime -float 0
  @defaults write com.apple.finder DisableAllAnimations -bool true
  @killall Finder
  @defaults write com.apple.dock autohide-time-modifier -float 0
  @defaults write com.apple.dock autohide-delay -float 0
  @defaults write com.apple.dock expose-animation-duration -float 0
  @killall Dock
